grammar life.game.GameDSL with org.eclipse.xtext.common.Terminals

generate gameDSL "http://www.game.life/GameDSL"

GoL:
	'Game' 'of' 'life:' grid=Grid? 
	('Initialize:' init += Initialization+)?
	('Rules:' rules += Evolution+)?;
	
Grid:
	'Grid' '(' width = INT ',' height = INT ')'
;

Percentage:
	number=INT '%'
;

Range:
	'(' p1=Point ',' p2=Point ')'
;

Point:
	'(' x = INT ',' y = INT ')'
;

Initialization:
	"Points:" points += Point+ |
	"Percentage:" percentage=Percentage |
	"Range:" ranges += Range+ |
	"Patterns:" patterns += Pattern+
;

enum COMPLEX:
	BLINKER = "blinker" |
	BLOCK = "block" |
	GLIDER = "glider"
;

Pattern:
	pattern = COMPLEX start=Point 
;

enum RULE:
	LIVE = 		'Live:' 	|
	DIE = 		'Die:'		|
	AWAKEN = 	'Awaken:'
;

Evolution:
	rule=RULE name='Percentage:' number=Percentage |
	rule=RULE name='Neighbors:' expr=Expr		  
;

Operator:
	'>='	|
	'>'		|
	'<'		|
	'<='	|
	'=='	|
	'!='
;

enum LOGICALOPERATOR:
	AND = 	'AND' |
	OR 	= 	'OR'
;

Expr:
	op=Operator number=INT  									|
	op=Operator number=INT logOp=LOGICALOPERATOR other=Expr 	
;

