grammar life.game.GameDSL with org.eclipse.xtext.common.Terminals

generate gameDSL "http://www.game.life/GameDSL"

GoL:
	'Game' 'of' 'life:'
	BEGIN
		grid=Grid?
		init=Initializations?
		rules=Evolutions?
	END
;
	
terminal BEGIN: 'synthetic:BEGIN';
terminal END: 'synthetic:END';
	
Grid:
	 
	'Grid' '(' width = INT ',' height = INT ')' 
;

Percentage:
	number=INT '%'
;

Range:
	'(' p1=Point ',' p2=Point ')'
;

Point:
	'(' x = INT ',' y = INT ')'
;

Initializations:
	'Initialize:' 
	BEGIN
		list += Initialization+ 
	END
;

Initialization:
	"Points:" BEGIN points += Point+ END			|
	"Percentage:" BEGIN percentage=Percentage  END	|
	"Range:" BEGIN ranges += Range+ END
;

enum RULE:
	LIVE = 		'Live:' 	|
	DIE = 		'Die:'		|
	AWAKEN = 	'Awaken:'
;

Evolutions:
	'Rules:' 
	BEGIN
		list += Evolution+
	END
;

Evolution:
	rule=RULE BEGIN name='Percentage:' BEGIN number=Percentage END END |
	rule=RULE BEGIN name='Neighbors:' BEGIN expr=Expr END END
;

Operator:
	'>='	|
	'>'		|
	'<'		|
	'<='	|
	'=='	|
	'!='
;

enum LOGICALOPERATOR:
	AND = 	'AND' |
	OR 	= 	'OR'
;

Expr:
	op=Operator number=INT  									|
	op=Operator number=INT logOp=LOGICALOPERATOR other=Expr 	
	
;

